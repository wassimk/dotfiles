#
# Colors
#

# Setup
# git clone https://github.com/chriskempson/base16-shell ~/.zsh/base16-shell
#
# Maintenance
# cd ~/.zsh/base16-shell
# git pull
# chmod +x *.sh
# and in Vim do a :PlugUpdate
# 
# Usage
# $ color help

BASE16_DIR=$HOME/.zsh/base16-shell
BASE16_CONFIG=$HOME/.vim/.base16

color() {
  BACKGROUND="$1"
  SCHEME="$2"

  if [ $# -eq 0 -a -s "$BASE16_CONFIG" ]; then
    cat ~/.vim/.base16
    return
  fi

  if [[ "$SCHEME" = 'help' ]]; then
    BACKGROUND='help'
  fi

  case "$BACKGROUND" in
  dark|light)
    FILE1="$BASE16_DIR/scripts/base16-$SCHEME-$BACKGROUND.sh"
    FILE2="$BASE16_DIR/scripts/base16-$SCHEME.sh"
    if [[ -x "$FILE1" ]]; then
      echo "$SCHEME" >! "$BASE16_CONFIG"
      echo "$BACKGROUND" >> "$BASE16_CONFIG"
      "$FILE1"
    elif [[ -x "$FILE2" ]]; then
      echo "$SCHEME" >! "$BASE16_CONFIG"
      echo "$BACKGROUND" >> "$BASE16_CONFIG"
      "$FILE2"
    else
      echo "Scheme '$SCHEME' not found in $BASE16_DIR"
      return 1
    fi
    ;;
  help)
    echo 'color dark [tomorrow|ocean|grayscale|ashes|default|railscasts|twilight|...]'
    echo 'color light [grayscale|harmonic16|ocean|tomorrow|twilight|...]'
    echo
    echo 'Available schemes:'
    find  "$BASE16_DIR" -name 'base16-*.sh' | \
      sed -E 's|.+/base16-||' | \
      sed -E 's/\-(dark|light)/ (\1)/' | \
      sed -E 's/\.sh//' | \
      column
      ;;

  *)
    echo 'Unknown subcommand: use one of {dark,light,help}'
    ;;
  esac

}

dark() {
  color dark "$1"
}

light() {
  color light "$1"
}

if [[ -s "$BASE16_CONFIG" ]]; then
  SCHEME=$(head -1 "$BASE16_CONFIG")
  BACKGROUND=$(sed -n -e '2 p' "$BASE16_CONFIG")
  if [ "$BACKGROUND" = 'dark' ]; then
    dark "$SCHEME"
  elif [ "$BACKGROUND" = 'light' ]; then
    light "$SCHEME"
  else
    echo "error: unknown background type in $BASE16_CONFIG"
  fi
else
  # Default.
  dark default
fi
