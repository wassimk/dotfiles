#
# Typora
#
typora() {
    if [ "$1" != "" ]
    then
      open -a typora $1
    else
      open -a typora "$HOME"/Dropbox/docs
    fi
}

#
# tmux
#

# completion for "ta" command in "$HOME"/.bin
function _ta() {
  for session in $(tmux list-sessions -F "#S"); do
    compadd "$session"
  done
}
compdef _ta ta

# completion for "tw" alias (ta work)
tw() { 
  (TMUX='' tmux new-session -Ad -s "$1" -c "$HOME"/Work/"$1")
  ta "$1"
}

function _tw() {
  for session in $(tmux list-sessions -F "#S"); do
    compadd -J "active" "$session"
  done

  for dir in $(ls -D -1 "$HOME"/Work); do
    compadd -J "inactive" "$dir"
  done
}
compdef _tw tw

# kill session
tk() {
    if [ "$1" != "" ]
    then
        tmux kill-session -t $1
    else
     echo "Kill which session?"
     echo ""
      tmux list-sessions -F "#S"
    fi
}
compdef _ta tk

# Make sure we are always in TMUX, uses "$HOME"/.bin/ta script
_not_inside_tmux() { [[ -z "$TMUX" ]] }

ensure_tmux_is_running() {
  if _not_inside_tmux; then
    if [ -d $DOTFILES ]; then
      cd $DOTFILES
      ta
    else
      ta
    fi
  fi
}

# Make CTRL-Z background and unbackground
function fg-bg() {
  if [[ $#BUFFER -eq 0 ]]; then
    fg
  else
    zle push-input
  fi
}
zle -N fg-bg
bindkey '^Z' fg-bg

#
# git
#
function g {
  if [[ $# > 0 ]]; then
    git $@
  else
    git status
  fi
}

# Complete g like git
compdef g=git

#
# Other Functions
#

# Auto complete for Planning Center Code directory
w() { cd "$HOME"/Work/"$1" && ll }
_w() { _files -J "files" -W "$HOME"/Work -/ }
compdef _w w

# Auto complete for Personal code directory
p() { cd "$HOME"/Personal/"$1" && ll }
_p() { _files -J "files" -W "$HOME"/Personal -/ }
compdef _p p

# Auto complete for Business code directory
b() { cd "$HOME"/Business/"$1" && ll }
_b() { _files -J "files" -W "$HOME"/Business -/ }
compdef _b b

# Auto complete for home directory
h() { cd "$HOME"/"$1" && la }
_h() { _files -J "files "-W "$HOME"/ -/ }
compdef _h h

# Console based weather report
weather() {
    if [ "$1" != "" ]
    then
        curl wttr.in/${1}
    else
        curl wttr.in/74037
    fi
}

# Aggregated man pages
cheat() {
    curl cheat.sh/"$1"
}

#
# Hooks
#
autoload -U add-zsh-hook

function -auto-ls-after-cd() {
  emulate -L zsh
  # Only in response to a user-initiated `cd`, not indirectly (eg. via another
  # function).
  if [ "$ZSH_EVAL_CONTEXT" = "toplevel:shfunc" ]; then
    ll
  fi
}
add-zsh-hook chpwd -auto-ls-after-cd
